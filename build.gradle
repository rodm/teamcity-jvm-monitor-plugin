
plugins {
    id 'com.github.rodm.teamcity-server' version '0.9.1'
    id 'org.sonarqube' version '2.0.1'
    id 'maven-publish'
}

group = 'com.github.rodm'
version = "1.0-SNAPSHOT"

ext {
    publicationVersion = System.properties['version'] ?: version
    teamcityVersion = System.properties['teamcity.version'] ?: '8.0.6'
    junitVersion = '4.11'

    vcsRevision = System.properties['build.vcs.number'] ?: "development"
    buildNumber = System.properties['build.number'] ?: new Date().format("yyyyMMddHHmmss")

    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
    teamcityJavaHome = System.properties['java.home']

    repositoryUrl = findProperty('repositoryUrl') ?: 'http://localhost:8080'
    repositoryUsername = findProperty('repositoryUsername') ?: ''
    repositoryPassword = findProperty('repositoryPassword') ?: ''
}

dependencies {
    agent project(path: ':agent', configuration: 'plugin')
    server project(':server')
}

teamcity {
    version = teamcityVersion

    server {
        descriptor = file("$rootDir/src/main/resources/teamcity-plugin.xml")
        tokens VERSION: project.version, VCS_REVISION: vcsRevision, BUILD_NUMBER: buildNumber

        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        environments {
            teamcity81 {
                version = '8.1.5'
            }

            teamcity91 {
                version = '9.1.6'
            }

            teamcity10 {
                version = '10-EAP2'
                downloadUrl = "https://download.jetbrains.com/teamcity/eap/TeamCity-41463.tar.gz"
                homeDir = file("${baseHomeDir}/TeamCity-${version}")
                dataDir = file("${baseDataDir}/10.0")
                javaHome = file(java8Home)
            }
        }
    }
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            version publicationVersion

            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
