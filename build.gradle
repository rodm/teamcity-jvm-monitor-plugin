
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.8'
    }
}

apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'maven-publish'

group = 'com.github.rodm'
version = '1.0-SNAPSHOT'

ext {
    teamcityVersion = System.properties['teamcity.version'] ?: '8.1.5'
    junitVersion = '4.11'
}

ext.teamcityDir = hasProperty('teamcity.home') ? property('teamcity.home') : "$rootDir/servers/TeamCity-${teamcityVersion}"
ext.teamcityDataDir = "$rootDir/data/" + (teamcityVersion =~ /(\d+\.\d+).*/)[0][1]
ext.teamcityJavaHome = System.properties['java.home']

ext.repositoryUrl = hasProperty('repositoryUrl') ? property('repositoryUrl') : 'http://localhost:8080'
ext.repositoryUsername = hasProperty('repositoryUsername') ? property('repositoryUsername') : ''
ext.repositoryPassword = hasProperty('repositoryPassword') ? property('repositoryPassword') : ''

def version = System.properties['version'] ?: "1.0-SNAPSHOT"
def vcsRevision = System.properties['build.vcs.number'] ?: "development"
def buildNumber = System.properties['build.number'] ?: new Date().format("yyyyMMddHHmmss")

dependencies {
    agent project(path: ':agent', configuration: 'plugin')
    server project(':server')
}

teamcity {
    version = teamcityVersion
    descriptor = file("$rootDir/src/main/resources/teamcity-plugin.xml")
    tokens VERSION: project.version, VCS_REVISION: vcsRevision, BUILD_NUMBER: buildNumber

    homeDir = file(teamcityDir)
    dataDir = file(teamcityDataDir)
    javaHome = file(teamcityJavaHome)
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
