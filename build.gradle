
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'

ext {
    teamCityVersion = System.properties['teamcity.version'] ?: '8.1.5'
    junitVersion = '4.11'
}

def version = System.properties['version'] ?: "1.0-SNAPSHOT"
def vcsRevision = System.properties['build.vcs.number'] ?: "development"
def buildNumber = System.properties['build.number'] ?: new Date().format("yyyyMMddHHmmss")

sourceCompatibility = 1.7
targetCompatibility = 1.7

processResources {
    filter(ReplaceTokens, tokens: [VERSION: version, VCS_REVISION: vcsRevision, BUILD_NUMBER: buildNumber])
}

task packagePlugin(type: Zip) {
    dependsOn ':agent:assemble', ':server:assemble'
    evaluationDependsOn 'agent'
    evaluationDependsOn 'server'

    from (project(':agent').distsDir) {
        into "agent"
    }
    from (project(':server').libsDir) {
        into "server"
    }
    from ("${buildDir}/resources/main") {
        include "teamcity-plugin.xml"
    }
}

assemble.dependsOn packagePlugin
