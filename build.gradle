
plugins {
    id 'com.github.rodm.teamcity-server' version '1.0'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'org.sonarqube' version '2.6.1'
}

group = 'com.github.rodm'
version = "1.0-SNAPSHOT"

ext {
    teamcityVersion = System.properties['teamcity.version'] ?: '10.0'
    junitVersion = '4.11'

    vcsRevision = System.properties['build.vcs.number'] ?: "development"
    buildNumber = System.properties['build.number'] ?: new Date().format("yyyyMMddHHmmss")

    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

dependencies {
    agent project(path: ':agent', configuration: 'plugin')
    server project(':server')
}

teamcity {
    version = teamcityVersion

    server {
        archiveName = "jvm-monitor-${rootProject.version}.zip"
        
        descriptor {
            name = 'jvmMonitorPlugin'
            displayName = 'JVM Monitor Plugin'
            version = rootProject.version
            description = 'Collects JVM metrics for any JVM running during the build and publishes them as artifacts'
            vendorName = 'Rod MacKenzie'
            vendorUrl = 'https://github.com/rodm/teamcity-jvm-monitor-plugin'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        teamcity10 {
            version = '10.0.5'
            javaHome = file(java8Home)
        }

        'teamcity2017.1' {
            version = '2017.1.5'
            javaHome = file(java8Home)
        }
    }
}

bintray {
    user = findProperty('bintray.user')
    key = findProperty('bintray.key')

    filesSpec {
        from "${buildDir}/distributions"
        into 'jvm-monitor'
        include '*.zip'
    }

    dryRun = false
    publish = true
    override = false

    pkg {
        repo = 'teamcity-plugins-generic'
        name = 'jvm-monitor'
        desc = 'A TeamCity plugin that provides a build feature to collect JVM metrics during a build'
        websiteUrl = 'https://github.com/rodm/teamcity-jvm-monitor-plugin'
        issueTrackerUrl = 'https://github.com/rodm/teamcity-jvm-monitor-plugin/issues'
        vcsUrl = 'https://github.com/rodm/teamcity-jvm-monitor-plugin.git'
        licenses = ['Apache-2.0']
        labels = ['teamcity', 'plugin', 'jvm', 'monitor']

        version {
            name = project.version
        }
    }
}
