
plugins {
    id 'com.github.rodm.teamcity-server' version '1.0'
    id 'org.sonarqube' version '2.6.1'
    id 'maven-publish'
}

group = 'com.github.rodm'
version = "1.0-SNAPSHOT"

ext {
    publicationVersion = System.properties['version'] ?: version
    teamcityVersion = System.properties['teamcity.version'] ?: '10.0'
    junitVersion = '4.11'

    vcsRevision = System.properties['build.vcs.number'] ?: "development"
    buildNumber = System.properties['build.number'] ?: new Date().format("yyyyMMddHHmmss")

    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_92'

    repositoryUrl = findProperty('repositoryUrl') ?: 'http://localhost:8080'
    repositoryUsername = findProperty('repositoryUsername') ?: ''
    repositoryPassword = findProperty('repositoryPassword') ?: ''
}

dependencies {
    agent project(path: ':agent', configuration: 'plugin')
    server project(':server')
}

teamcity {
    version = teamcityVersion

    server {
        descriptor = file("$rootDir/src/main/resources/teamcity-plugin.xml")
        tokens VERSION: project.version, VCS_REVISION: vcsRevision, BUILD_NUMBER: buildNumber
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        teamcity10 {
            version = '10.0.5'
            javaHome = file(java8Home)
        }

        'teamcity2017.1' {
            version = '2017.1.5'
            javaHome = file(java8Home)
        }
    }
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            version publicationVersion

            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
